group 'net.eraga.rxusb'
version "$version"

buildscript {
    ext.rxJava_version = '2.1.13'

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.researchgate:gradle-release:2.8.1'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven-publish'
apply plugin: 'signing'


sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "io.reactivex.rxjava2:rxjava:$rxJava_version"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
}


task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "Reactive USB high level facade library"
                description 'RxUsb is a cross-platform USB facade library'
                url = "https://github.com/eraga/rxusb"

                scm {
                    url = "scm:git@github.com:eraga/rxusb.git"
                    connection = "scm:git@github.com:eraga/rxusb.git"
                    developerConnection = "scm:git@github.com:eraga/rxusb.git"
                }

                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        //noinspection GroovyAssignabilityCheck
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "tntclaus"
                        name = "Klaus Schwartz"
                        email =  "tntclaus@gmail.com"
                    }
                }
            }

            groupId group
            artifactId archivesBaseName
            version version
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            def repositoryUrl = 'https://packages.eraga.net/repository/eraga-public-maven-releases/'
            def snapshotRepositoryUrl = 'https://packages.eraga.net/repository/eraga-public-maven-unstable/'
            url !version.endsWith("SNAPSHOT") ? repositoryUrl : snapshotRepositoryUrl
            credentials {
                username = "$nexusUsername"
                password = "$nexusPassword"
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { !version.endsWith("SNAPSHOT") }
}


release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = ''
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '$name-$version'
    versionPropertyFile = 'gradle.properties'
    versionProperties = []
    buildTasks = ['build']

    //noinspection UnnecessaryQualifiedReference
    scmAdapters = [
            net.researchgate.release.GitAdapter
    ]

    git {
        requireBranch = 'release'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
//        signTag = false
    }


}
